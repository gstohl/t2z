plugins {
    id 'java-library'
    id 'com.vanniktech.maven.publish' version '0.30.0'
}

group = 'io.github.gstohl'
version = System.getenv('VERSION')?.replaceFirst('^v', '') ?: '0.1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // JNA for FFI bindings
    implementation 'net.java.dev.jna:jna:5.14.0'

    // Bouncy Castle for secp256k1 signing
    implementation 'org.bouncycastle:bcprov-jdk18on:1.77'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

mavenPublishing {
    publishToMavenCentral(com.vanniktech.maven.publish.SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()

    coordinates('io.github.gstohl', 't2z-java', version)

    pom {
        name = 't2z-java'
        description = 'Java bindings for t2z (Transparent to Zcash) library - enabling transparent Zcash wallets to send shielded Orchard outputs via PCZT'
        url = 'https://github.com/gstohl/t2z'
        inceptionYear = '2024'

        licenses {
            license {
                name = 'MIT License'
                url = 'https://opensource.org/licenses/MIT'
            }
        }

        developers {
            developer {
                id = 'gstohl'
                name = 'Dominik Gst√∂hl'
            }
        }

        scm {
            connection = 'scm:git:git://github.com/gstohl/t2z.git'
            developerConnection = 'scm:git:ssh://github.com/gstohl/t2z.git'
            url = 'https://github.com/gstohl/t2z'
        }
    }
}
