plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.gstohl'
version = System.getenv('VERSION')?.replaceFirst('^v', '') ?: '0.1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    // JNA for FFI bindings
    implementation 'net.java.dev.jna:jna:5.14.0'

    // Bouncy Castle for secp256k1 signing
    implementation 'org.bouncycastle:bcprov-jdk18on:1.77'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 't2z-java'
            from components.java

            pom {
                name = 't2z-java'
                description = 'Java bindings for t2z (Transparent to Zcash) library - enabling transparent Zcash wallets to send shielded Orchard outputs via PCZT'
                url = 'https://github.com/gstohl/t2z'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'gstohl'
                        name = 'Dominik Gst√∂hl'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/gstohl/t2z.git'
                    developerConnection = 'scm:git:ssh://github.com/gstohl/t2z.git'
                    url = 'https://github.com/gstohl/t2z'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'OSSRH'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = System.getenv('OSSRH_USERNAME')
                password = System.getenv('OSSRH_PASSWORD')
            }
        }
    }
}

signing {
    def signingKey = System.getenv('GPG_PRIVATE_KEY')
    def signingPassword = System.getenv('GPG_PASSPHRASE')
    if (signingKey != null && signingPassword != null) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
    }
}
